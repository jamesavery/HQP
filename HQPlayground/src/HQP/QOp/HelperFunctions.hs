module HQP.QOp.HelperFunctions where
import HQP.QOp.Syntax hiding (I)
import Data.Bits(FiniteBits,finiteBitSize,countLeadingZeros,countTrailingZeros,shiftL,shiftR)
import Data.List (sort)
import Math.NumberTheory.Logarithms (integerLog2')
import Numeric.LinearAlgebra hiding (scale, (<>), trace)
import qualified Data.PQueue.Prio.Min as PriorityQ
import qualified Data.Vector as V
import qualified Data.Vector.Generic as G
import qualified Data.Vector.Mutable as MV
import qualified Data.Set as S
import Control.Monad.ST (runST)


-- | Signature of an operator a: C^{2^m} -> C^{2^n} is (m,n) = (op_domain a, op_range a)


op_dimension :: QOp -> Nat
op_dimension op = 1 `shiftL` (op_qubits op)

step_qubits :: Step -> Nat
step_qubits step = case step of 
  Unitary op -> op_qubits op
  Measure ks      -> 1 + foldr max 0 ks
  Initialize ks _ -> 1 + foldr max 0 ks

           
prog_qubits :: Program -> Nat
prog_qubits program = maximum $ map step_qubits program

-- | Support of an operator: the list of qubits it acts non-trivially on.
op_support :: QOp -> S.Set Nat
op_support op = let
    shift ns k   = S.map (+k) ns
    union xs ys  = S.union xs ys
  in case op of
  Id _          -> S.empty
  Phase _       -> S.empty
  R _ 0         -> S.empty
  R a _         -> op_support a
  C a           -> S.insert 0 ((op_support a) `shift` 1)
  Tensor a b    -> (op_support a) `union` ((op_support b) `shift` (op_qubits a))
  DirectSum a b -> S.insert 0 ((op_support a `union` op_support b) `shift` 1)
  -- Compose's support is the union of its operands'. (Earlier special cases for
  -- Permute were unsound — e.g. `Compose (Permute ks) (Id n)` returned ∅ instead of
  -- permSupport ks, because the bits Permute moves count toward support even when the
  -- other side has empty support.)
  Compose a b   -> union (op_support a) (op_support b)
  Adjoint a     -> op_support a
  Permute ks    -> S.fromList $ permSupport ks
  _             -> S.singleton 0 -- 1-qubit gates


-- Small helper functions
toBits :: (Integral a) => a -> [Nat]
toBits 0 = []
toBits k = (toBits (k `div` 2)) ++ [fromIntegral (k `mod` 2)]

toBits' :: Integral t => Int -> t -> [Nat]
toBits' n k = let 
    bits = toBits k
    m    = length bits
  in
    (replicate (n-m) 0) ++ bits

fromBits :: [Int] -> Int
fromBits bs =
        foldl (\acc b -> (acc `shiftL` 1) + b) 0 bs  -- MSB-first decode

-- | Infinite list of powers of two, constructed with O(1) time per element
powersOfTwo :: [Nat]
powersOfTwo = iterate (*2) 1

dotlists :: Num a => [a] -> [a] -> a
dotlists xs ys = sum $ zipWith (*) xs ys

bitIndexMSB, bitIndexLSB :: [Int] -> Nat
bitIndexMSB ks = dotlists (reverse ks) powersOfTwo
bitIndexLSB ks = dotlists ks powersOfTwo


-- | ilog2 m = floor (log2 m) for m >= 0

ilog2 :: (FiniteBits a, Integral a) => a -> Nat
ilog2 m = finiteBitSize m - countLeadingZeros m - 1

integerlog2 :: Integer -> Int
integerlog2 = integerLog2'

pow2 :: Nat -> Nat
pow2 n = 1 `shiftL` n

-- | ceil_log2 m = ⌈log2 m⌉ for m >= 1; the smallest k with pow2 k >= m.
--   I.e. the number of bits needed to index m distinct items: 1→0, 2→1, 3→2, 4→2, 5→3, ...
--   Returns 0 for m <= 1 (defensive; log2 0 is undefined).
ceil_log2 :: (FiniteBits a, Integral a) => a -> Nat
ceil_log2 m
  | m <= 1    = 0
  | otherwise = ilog2 (m - 1) + 1

evenOdd :: [a] -> ([a],[a])
evenOdd [] = ([],[])
evenOdd [x] = ([x],[])
evenOdd (x:y:xs) = let (es,os) = evenOdd xs in (x:es,y:os)

-- | Working with permutations
permApply :: [Int] -> [a] -> [a]
permApply ks xs = [ xs !! k | k <- ks ]

permSupport :: [Int] -> [Int]
permSupport ks = [ i | (i,j) <- zip [0..] ks, i /= j ]

invertPerm :: [Int] -> [Int] -- TODO: invertPerm -> permInvert for consistency
invertPerm ks = map snd $  -- For each index in the output, find its position in the input
    sort [ (k, i) | (i, k) <- zip [0..] ks ]



-- | Minimal adjacent-swap indices (0-based) sending permutation p to identity.
--   Swap index i means swapping positions i and i+1.
--
--   For v = 0..n-1, move value v left until it sits at position v.
--   Each step performs exactly the inversions involving v, so total swap count is minimal,
--   one of very few applications where bubble sort is optimal.
--
-- Uses locally mutable vectors to avoid the  O(n^2) worst case unless it is actually needed:
-- Θ(n+inv(p)) instead of Θ(n^2+inv(p)) = O(n^2).
permutationSwaps :: V.Vector Int -> [Int]
permutationSwaps p0 = runST $ do
  let n    = V.length p0
      -- inverse permutation: pos[v] = index where value v currently sits.
      -- V.indexed p0 = [(i, p0!i)]; we need pairs (p0!i, i) to update pos[v] = i.
      pos0 = V.update (V.replicate n 0) (V.imap (\i v -> (v, i)) p0)
  p   <- V.thaw p0
  pos <- V.thaw pos0

  let -- adjacent swap s_i: swap p[i],p[i+1] and update pos accordingly
      swapAt i = do -- Updates mutable vectors p and pos to swap values at positions i and i+1
        a <- MV.read p i; b <- MV.read p (i+1)
        MV.write p i b;   MV.write p (i+1) a
        MV.write pos a (i+1); MV.write pos b i

      -- bubble value v left by s_{k-1} ... s_v, where k = pos[v]
      bubble v k swaps
        | k <= v    = pure swaps
        | otherwise = let i = k-1 in swapAt i >> bubble v i (i:swaps)

      -- Bubble sort remaining values v..n-1, assuming positions 0..v-1 are already sorted: 
      -- p[j]=j for all j < v,
      -- while producing the list of swaps performed.
      sortFrom v swaps
        | v >= n    = pure (reverse swaps)
        | otherwise = MV.read pos v 
                  >>= \k -> bubble v k swaps 
                  >>= sortFrom (v+1)

  sortFrom 0 []


-- HMatrix helper functions
split2x2 :: Element e
         => Nat      -- nr row split
         -> Nat      -- nc column split
         -> Matrix e -- m  input matrix
         -> (Matrix e, Matrix e, Matrix e, Matrix e)
split2x2 nr nc m = let
    (r,c) = (rows m, cols m)
  in
    ( subMatrix (0,0)   (nr,  nc)   m,
      subMatrix (0,nc)  (nr,  c-nc) m,
      subMatrix (nr,0)  (r-nr,nc)   m,
      subMatrix (nr,nc) (r-nr,c-nc) m  )

split2x1 :: Element e 
         => Nat      -- nr row split
         -> Matrix e -- m  input matrix
         -> (Matrix e, Matrix e)
split2x1 nr m = let
    (r,c) = (rows m, cols m)
  in
    ( subMatrix (0,0)   (nr,  c) m,
      subMatrix (nr,0)  (r-nr,c) m  )

split1x2 ::Element e 
         =>  Nat      -- nc column split
         -> Matrix e -- m  input matrix
         -> (Matrix e, Matrix e)
split1x2 nc m = let
    (r,c) = (rows m, cols m)
  in
    ( subMatrix (0,0)   (r,  nc)   m,
      subMatrix (0,nc)  (r,  c-nc) m  )   

  -- s descending. Return first i with s!i < eps2, or n if none.
firstBelow :: (G.Vector v a, Ord a) => a -> v a -> Int
firstBelow cutoff s = go 0 (G.length s)
  where
    go !lo !hi
      | lo >= hi           = lo
      | s G.! mid < cutoff = go lo mid
      | otherwise          = go (mid+1) hi
      where
        !mid = (lo+hi) `div` 2   


-- | Fold a binary operator over a vector via a balanced binary-tree shape
--   rather than the linear left/right chain of `foldl1`/`foldr1`. Length must
--   be a positive power of 2. Useful when the operator's result-arity grows
--   with fold depth (e.g. `foldBalanced v DirectSum` has depth log₂(length v)
--   instead of length v − 1). No equivalent exists in the Haskell base
--   libraries; `mconcat`/`foldMap` and friends use a linear fold by default.
foldBalanced :: V.Vector t -> (t -> t -> t) -> t
foldBalanced v f = go v
  where
    go xs
      | V.length xs == 1 = V.head xs
      | otherwise        = go (V.generate (V.length xs `div` 2) $ \i ->
                                  f (xs V.! (2*i)) (xs V.! (2*i+1)))


-- HELPER DATA STRUCTURES
pushTopK :: Int 
         -> Double 
         -> a -> PriorityQ.MinPQueue Double a 
         -> PriorityQ.MinPQueue Double a
pushTopK !k !key !val !q
  | k <= 0        = PriorityQ.empty
  | PriorityQ.size q < k = PriorityQ.insert key val q
  | otherwise     =
      let (!kmin, _) = PriorityQ.findMin q
      in  if key <= kmin then q else PriorityQ.insert key val (PriorityQ.deleteMin q)
